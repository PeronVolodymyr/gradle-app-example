import com.example.gradle.JarCount

// Custom convention plugin - mainly composition of another plugins.
// Plugins that only reconfigure existing plugins are typically called 'convention plugin'.
plugins {
    id("application")
    id("my-java-base")
}

// Custom task based on custom 'JarCount.java' task implementation.
// This will count all jar files generated by this application.
tasks.register<JarCount>("countJars") {
    group = "My group"
    description = "Count all jars"

    // Input -> task.jar and all dependencies.
    allJars.from(tasks.jar)
    allJars.from(configurations.runtimeClasspath)

    // Output -> "gen/count.txt" file.
    countFile.set(layout.buildDirectory.file("gen/count.txt"))
}

// Custom task based on ready (gradle provided) 'Zip.java' task implementation.
// This will zip all jar files generated by this application.
tasks.register<Zip>("bundle") {
    // optionally we can group tasks
    group = "My group"
    description = "packages it all!"

    // zip the output of the 'jar' task.
    from(tasks.jar)
    // zip all the dependencies of 'jar' task. (commons.lang3 in our case).
    from(configurations.runtimeClasspath)

    // save output of this task (zip file) into build directory and create 'distribution' folder there.
    destinationDirectory.set(layout.buildDirectory.dir("distribution"))
}